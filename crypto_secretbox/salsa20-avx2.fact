void _salsa_keysetup(
    secret mut uint32[16] ctx,
    secret uint8[32] k) {
  public uint32[16] TR = { 0, 5, 10, 15, 12, 1, 6, 11, 8, 13, 2, 7, 4, 9, 14, 3 };
  ctx[TR[1]]  = _load32_le(arrview(k,  0, 4));
  ctx[TR[2]]  = _load32_le(arrview(k,  4, 4));
  ctx[TR[3]]  = _load32_le(arrview(k,  8, 4));
  ctx[TR[4]]  = _load32_le(arrview(k, 12, 4));
  ctx[TR[11]] = _load32_le(arrview(k, 16, 4));
  ctx[TR[12]] = _load32_le(arrview(k, 20, 4));
  ctx[TR[13]] = _load32_le(arrview(k, 24, 4));
  ctx[TR[14]] = _load32_le(arrview(k, 28, 4));
  ctx[TR[0]]  = 0x61707865;
  ctx[TR[5]]  = 0x3320646e;
  ctx[TR[10]] = 0x79622d32;
  ctx[TR[15]] = 0x6b206574;
}

void _salsa_ivsetup(
    secret mut uint32[16] ctx,
    secret uint8[8] iv,
    public uint8[8] counter) {
  public uint32[16] TR = { 0, 5, 10, 15, 12, 1, 6, 11, 8, 13, 2, 7, 4, 9, 14, 3 };
  ctx[TR[6]] = _load32_le(arrview(iv, 0, 4));
  ctx[TR[7]] = _load32_le(arrview(iv, 4, 4));
  ctx[TR[8]] = _load32_le(arrview(counter, 0, 4));
  ctx[TR[9]] = _load32_le(arrview(counter, 4, 4));
}

void _salsa_ivsetup_null(
    secret mut uint32[16] ctx,
    secret uint8[8] iv) {
  public uint32[16] TR = { 0, 5, 10, 15, 12, 1, 6, 11, 8, 13, 2, 7, 4, 9, 14, 3 };
  ctx[TR[6]] = _load32_le(arrview(iv, 0, 4));
  ctx[TR[7]] = _load32_le(arrview(iv, 4, 4));
  ctx[TR[8]] = 0;
  ctx[TR[9]] = 0;
}

void _salsa20_encrypt_bytes(
    secret mut uint32[16] ctx,
    secret uint8[] m,
    secret mut uint8[] c) {
  u0(ref ctx, m, ref c);
}

public int32
_crypto_stream_salsa20(
    secret mut uint8[] c,
    public uint8[8] n,
    secret uint8[32] k) {
  if (len c == 0) {
    return 0;
  }

  secret mut uint32[16] ctx = noinit(16);
  _salsa_keysetup(ref ctx, k);
  _salsa_ivsetup_null(ref ctx, n);

  _memzero(ref c);
  _salsa20_encrypt_bytes(ref ctx, c, ref c);

  _memzero32(ref ctx);
  return 0;
}

public int32
_crypto_stream_salsa20_xor_ic(
    secret mut uint8[] c,
    secret uint8[] m,
    secret uint8[8] n,
    public uint64 ic_val,
    secret uint8[32] k) {
  if (len m == 0) {
    return 0;
  }

  public mut uint8[8] ic_bytes = noinit(8);
  _store64_le_public(ref ic_bytes, ic_val);

  secret mut uint32[16] ctx = noinit(16);
  _salsa_keysetup(ref ctx, k);
  _salsa_ivsetup(ref ctx, n, ic_bytes);
  _salsa20_encrypt_bytes(ref ctx, m, ref c);

  _memzero32(ref ctx);
  return 0;
}
